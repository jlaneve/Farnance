app Farnance {
  title: "Farnance",

  head: [
    "<link rel=\"stylesheet\" href=\"https://fonts.googleapis.com/css?family=Roboto:300,400,500&display=swap\" />"
  ]
}

// Authentication

auth {
  userEntity: User,
  methods: [ EmailAndPassword ],
  onAuthFailedRedirectTo: "/login"
}

// Entities

entity User {=psl
    id                Int       @id @default(autoincrement())
    username          String    @unique
    email             String    @unique
    password          String

    farmer            Boolean   @default(true)

    products          Product[]
psl=}


entity Product {=psl
    id                Int       @id @default(autoincrement())
    type              String
    name              String    @default("")
    quality           String
    quantity          Int

    owner             User      @relation(fields: [ownerId], references: [id])
    ownerId           Int

    financingAgreement FinancingAgreement @relation(fields: [agreementId], references: [id])
    agreementId       Int
psl=}


entity FinancingAgreement {=psl
    id                Int         @id @default(autoincrement())

    filled            Boolean     @default(false)

    amount            Float
    rate              Float

    // Before order is filled
    productType       String
    quality           String


    // Only available after order is filled
    products          Product[]

    bank              User        @relation(fields: [bankId], references: [id])
    bankId            Int
psl=}



// Pages for all
route "/" -> page Landing
page Landing {
  component: import Landing from "@ext/pages/Landing/Landing.jsx"
}


route "/register" -> page Register
page Register {
  component: import Register from "@ext/pages/Register/Register.jsx"
}

route "/login" -> page Login
page Login {
  component: import Login from "@ext/pages/Login/Login.jsx"
}

route "/home" -> page Home
page Home {
  authRequired: true,
  component: import Home from "@ext/pages/Home/Home.jsx"
}

// Farmer pages
route "/new-product" -> page NewProduct
page NewProduct {
  authRequired: true,
  component: import NewProduct from "@ext/pages/Farmers/NewProduct/NewProduct.jsx"
}


// Bank pages
route "/new-offer" -> page NewOffer
page NewOffer {
  authRequired: true,
  component: import NewOffer from "@ext/pages/Banks/NewOffer.jsx"
}

// Actions

action createProduct {
  fn: import { createProduct } from "@ext/actions.js",
  entities: [Product]
}

action createOffer {
  fn: import { createOffer } from "@ext/actions.js",
  entities: [FinancingAgreement]
}


// Queries

query getProducts {
  fn: import { getProducts } from "@ext/queries.js",
  entities: [Product]
}


// Dependencies

dependencies {=json
  "@material-ui/core": "^4.11.1",
  "@material-ui/data-grid": "latest"
json=}